import io.miret.etienne.gradle.sass.CompileSass
import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:2.3'
    }
}

plugins {
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'jacoco'
    id "org.flywaydb.flyway" version "[9.+,10.0["
    id 'io.miret.etienne.sass' version '1.5.1'
    id "io.sentry.jvm.gradle" version "4.13.0"
}

jacoco {
    toolVersion = '0.8.10'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def props = new Properties()
if (file(".env").exists()) {
    file(".env").withInputStream { props.load(it) }
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    mavenCentral()
}

def profile = props.getProperty('SPRING_PROFILES_ACTIVE')
def formFlowLibraryVersion = '1.4.4'
def useLocalLibrary = props.getProperty('USE_LOCAL_LIBRARY')

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.3.5'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'org.jobrunr:jobrunr-spring-boot-3-starter:7.3.1'
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation 'software.amazon.awssdk:s3:2.29.12'

    if (useLocalLibrary == 'true') {
        implementation fileTree(dir: "$rootDir/../form-flow/build/libs", include: '*.jar')
        println "📦 Using local library"
    } else {
        implementation "org.codeforamerica.platform:form-flow:${formFlowLibraryVersion}"
        println "📚Using form flow library ${formFlowLibraryVersion}"
    }

    compileOnly 'org.projectlombok:lombok:1.18.34'

    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.3.5'

    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.3.5'
    testImplementation 'org.springframework.security:spring-security-test:6.3.4'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.26.0'
    testImplementation 'io.percy:percy-java-selenium:2.1.0'
    testImplementation 'org.jsoup:jsoup:1.18.1'
    testImplementation 'org.awaitility:awaitility:4.2.2'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.9.2'
    testImplementation 'com.h2database:h2:2.3.232'
    testImplementation 'com.deque.html.axe-core:selenium:4.10.1'
    testImplementation 'org.projectlombok:lombok:1.18.34'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:3.5.0'

    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql:42.7.4'
}

springBoot {
    buildInfo()
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

// -------------------------------------
// USWDS Tasks
// -------------------------------------
sass {
    // Version of dart-sass: https://github.com/sass/dart-sass/releases
    version = '1.69.3'
}

tasks.register('checkForNpm', Exec) {
    group 'cfa-uswds'
    description = 'Check if npm is accessible and install it if not.'
    commandLine 'sh', './scripts/check_for_npm.sh'
}

tasks.register('npmInstall', Exec) {
    group 'cfa-uswds'
    description = 'Install frontend dependencies using npm.'
    dependsOn 'checkForNpm'
    commandLine 'npm', 'install'
}

tasks.register('moveNodeModulesToGenerated', Copy) {
    group 'cfa-uswds'
    description = 'Move Node Modules into Generated so that Spring Boot can use the assets.'
    dependsOn 'npmInstall'
    from 'node_modules'
    into 'generated/main/resources/static'
    finalizedBy(moveUSWDSImagesToGenerated)
}

tasks.register('moveUSWDSImagesToGenerated', Copy) {
    group 'cfa-uswds'
    description = 'Move USWDS images into generated folder for Spring Boot to use it.'
    dependsOn 'moveNodeModulesToGenerated'
    from 'node_modules/@uswds/uswds/dist/img'
    into 'generated/main/resources/static/assets/css/img'
}

def sassConfiguration = { CompileSass task ->
    // Load path
    task.loadPath project.file("${projectDir}/generated/main/resources/static")
    task.loadPath project.file("${projectDir}/generated/main/resources/static/@codeforamerica/uswds/packages")
    task.loadPath project.file("${projectDir}/generated/main/resources/static/@uswds/uswds/packages")
    // Set the output style:
    // Possible values are “expanded” and “compressed”, default is “expanded”.
    task.style = 'expanded'
    // For now, do not load source maps
    task.sourceMap = 'none'
    // Source directory containing sass to compile:
    task.sourceDir = project.file("${projectDir}/src/main/resources/static/assets/scss")
    // Directory where to output generated CSS:
    task.outputDir = project.file("${projectDir}/generated/main/resources/static/assets/css")
}

test {
    useJUnitPlatform {
        JUnitPlatformOptions options ->
            options.excludeTags("a11y")
    }
}

tasks.register("accessibilityTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'a11y'
        }
}

tasks.withType(Test).configureEach {
    environment("SENTRY_DSN", "")
    environment("MIXPANEL_API_KEY", "this-is-a-dummy-key-for-tests")
}

tasks.named('compileSass', CompileSass).configure { task ->
    group 'cfa-uswds'
    description = 'Compile Sass files into CSS.'
    dependsOn 'moveNodeModulesToGenerated'
    sassConfiguration(task)
}

tasks.register('compileJs', Exec) {
    group 'cfa-uswds'
    description = 'Compile CommonJS into JavaScript.'
    dependsOn 'moveNodeModulesToGenerated'
    commandLine 'npx', 'rollup', '--config', 'rollup.config.cjs'
}

tasks.register('watchCompileSass', CompileSass) { task ->
    group 'cfa-uswds watch'
    description = 'Infinite task to watch and recompile changes with SCSS.'
    dependsOn installSass
    sassConfiguration(task)
    watch()
}

tasks.register('watchCompileJs', Exec) {
    group 'cfa-uswds watch'
    description = 'Infinite task to watch and recompile changes with JavaScript.'
    dependsOn 'moveNodeModulesToGenerated'
    commandLine 'npx', 'rollup', '--config', 'rollup.config.cjs', '--watch'
}

tasks.register('cleanGenerated') {
    group 'cfa-uswds'
    description = 'Delete the generated directory.'
    doFirst {
        delete "generated"
    }
}

clean {
    doFirst {
        delete "generated"
        delete "node_modules"
    }
}

enum DesignSystem {
    HONEYCRISP,
    CFA_USWDS;

    static DesignSystem fromString(String value) {
        for (DesignSystem designSystem : values()) {
            if (designSystem.name().replaceAll('_', '-').equalsIgnoreCase(value)) {
                return designSystem
            }
        }
        throw new IllegalArgumentException("Unknown design system: " + value)
    }
}

def applicationYaml = file('src/main/resources/application.yaml')
def applicationProps = new Yaml().load(applicationYaml.text)
def designSystemNameString = applicationProps.get('form-flow')?.get('design-system')?.get('name')
def designSystem = DesignSystem.fromString(designSystemNameString ?: 'HONEYCRISP')
println "🎨 Using design system: $designSystem"

processResources {
    if (designSystem == DesignSystem.CFA_USWDS) {
        dependsOn compileSass
        dependsOn compileJs
    }
}


jar {
    enabled false
}
